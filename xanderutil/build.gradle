apply plugin: 'com.android.library'

//配置插件

//apply plugin: 'com.novoda.bintray-release'//添加
//配置插件
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"//这个是版本号，必须填写

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

def siteUrl = 'https://github.com/Xander1024'      // 项目的主页   这个是说明，可随便填
def gitUrl = 'https://github.com/Xander1024'      // Git仓库的url  这个是说明，可随便填
group = "com.smart"    // 这里是groupId ,必须填写  一般填你唯一的包名，一旦写好好，就不要修改了

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Android Custom ViewHolder'     //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'ouyangmuyuan'        //填写开发者的一些基本信息
                        name 'ouyangmuyuan'    //填写开发者的一些基本信息
                        email '596928539@qq.com'   //填写开发者的一些基本信息
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "xander"    //发布到JCenter上的项目名字，必须填写(新库修改)=================================================================================================
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

////添加
//publish {
//    userOrg = 'ouyangmuyuan'//bintray.com用户名
//    groupId = 'com.util'//jcenter上的路径
//    artifactId = 'xanderutil'//在jcenter上maven‘util’下创建的报名
//    publishVersion = '1.0.0'//版本号
//    desc = 'Oh hi, this is a nice description for a project, right?'//描述
//    website = 'https://github.com/Xander1024'//网站
//}
